plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.dua3.gradle.runtime' version '1.13.1-patch-1'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.0'
}

group = 'com.fadlan'
version = '2.0.1'

repositories {
    mavenCentral()
    maven { url = "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
}

ext {
    junitVersion = '5.10.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.fadlan.fireporter.FireporterApp'
    applicationDefaultJvmArgs = [
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
            '--add-opens=java.base/java.util=ALL-UNNAMED',
            "--add-opens=java.desktop/java.awt=ALL-UNNAMED",
            "--add-opens=java.desktop/sun.awt=ALL-UNNAMED",
    ]
}

kotlin {
    jvmToolchain(21)
}

javafx {
    version = '21.0.7'
    modules = [
            'javafx.base',
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics',
            'javafx.media'
    ]
}

dependencies {
    implementation 'io.ktor:ktor-client-core-jvm:3.1.3'
    implementation 'io.ktor:ktor-client-apache:3.1.3'
    runtimeOnly files('lib/jetbrains-mono-font-ext.jar')
    runtimeOnly files('lib/montserrat-font-ext.jar')
    runtimeOnly files('lib/roboto-font-ext.jar')

    implementation("org.codehaus.groovy:groovy:3.0.9")

    testImplementation("io.kotest:kotest-runner-junit5:5.9.0")
    testImplementation("io.kotest:kotest-assertions-core:5.9.0")
    testImplementation("io.kotest:kotest-framework-engine:5.9.0")

    implementation(project.dependencies.platform("io.insert-koin:koin-bom:4.0.3"))
    implementation("io.insert-koin:koin-core")

    implementation("org.slf4j:slf4j-api:2.0.13")
    runtimeOnly("org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0")
    runtimeOnly("org.apache.logging.log4j:log4j-core:2.20.0")
    runtimeOnly("org.apache.logging.log4j:log4j-api:2.20.0")

    implementation("io.github.mkpaz:atlantafx-base:2.0.0")

    implementation("io.ktor:ktor-client-cio:3.1.3")
    implementation("io.ktor:ktor-client-content-negotiation:3.1.3")
    implementation("io.ktor:ktor-serialization-kotlinx-json:3.1.3")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.8.0")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.1")

    implementation("org.apache.pdfbox:pdfbox:3.0.5")
    implementation("org.apache.pdfbox:pdfbox-tools:3.0.5")
    implementation("org.apache.pdfbox:jbig2-imageio:3.0.4")

    implementation("commons-logging:commons-logging:1.2") {
        exclude group: "org.bouncycastle"
    }

    implementation("net.sf.jasperreports:jasperreports:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-functions:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-fonts:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-pdf:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-excel-poi:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-chart-themes:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-charts:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-chart-customizers:7.0.2")
    implementation("net.sf.jasperreports:jasperreports-groovy:7.0.2")

    implementation("com.github.librepdf:openpdf:2.0.5")

    implementation("com.fasterxml.jackson.core:jackson-core:2.17.1")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.17.1")
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.17.1")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.1")

    runtimeOnly("io.projectreactor.tools:blockhound:1.0.8.RELEASE")
}

configurations.configureEach {
    exclude group: "org.bouncycastle", module: "bcprov-jdk14"
    exclude group: 'xml-apis', module: 'xml-apis'
    exclude group: "org.springframework", module: "spring-jcl"
    resolutionStrategy.eachDependency {
        if (requested.group == "org.codehaus.groovy") {
            useTarget("org.apache.groovy:${requested.name}:${requested.version}")
            because("Avoid conflicts between org.codehaus.groovy and org.apache.groovy")
        }
    }
}

sourceSets {
    main {
        java.srcDirs("src/main/kotlin")
        resources.srcDirs("src/main/resources")
    }
    test {
        java.srcDirs("src/test/kotlin")
        resources.srcDirs("src/test/resources")
    }
}

test {
    useJUnitPlatform()
}

tasks.test {
    useJUnitPlatform()
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    filesMatching('**/app.properties') {
        expand(version: project.version)
    }
}

runtime {
    launcher {
        noConsole = true
    }
    options = [
            '--compress', '2', '--no-header-files', '--no-man-pages',
    ]
    modules = [
            'java.base',
            'java.desktop',
            'jdk.unsupported',
            'java.logging',
            'java.xml',
            'java.sql',
            'javafx.base',
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics',
            'javafx.media'
    ]
    additive = true

    targetPlatform("linux") { // linux x64
        jdkHome = jdkDownload("https://is3.cloudhost.id/jdk-javafx-bundle/21/jdk21-linux_x64.tar.gz") {
            archiveName = "linux-x64-jdk21"
        }
    }

    targetPlatform("mac") { // mac x64
        jdkHome = jdkDownload("https://is3.cloudhost.id/jdk-javafx-bundle/21/jdk21-macos_x64.tar.gz") {
            archiveName = "mac-x64-jdk21"
        }
    }

    targetPlatform("mac-aarch64") { // mac arm
        jdkHome = jdkDownload("https://is3.cloudhost.id/jdk-javafx-bundle/21/jdk21-macos_aarch64.tar.gz") {
            archiveName = "mac-aarch64-jdk21"
        }
    }

    targetPlatform("win") { // win x64
        jdkHome = jdkDownload("https://is3.cloudhost.id/jdk-javafx-bundle/21/jdk21-windows_x64.zip") {
            archiveName = "windows-x64-jdk21"
        }
    }

    jpackage {
        imageOutputDir = file("$buildDir/distributions/image") as File
        installerOutputDir = file("$buildDir/distributions/installer") as File

        imageName = "Fireporter"
        installerOptions = [
                '--vendor', 'Fadlan Abduh',
        ]
        appVersion = project.version
        resourceDir = file("src/main/resources") as File

        def currentOs = org.gradle.internal.os.OperatingSystem.current()

        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/icons/fireporter.$imgType"]

        if(currentOs.windows) {
            targetPlatformName = "win"
            installerType = "msi"
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (currentOs.linux) {
            targetPlatformName = "linux"
            installerType = "deb"
            imageName = "fireporter"
            installerOptions += ['--linux-package-name', 'fireporter', '--linux-shortcut']
        } else if (currentOs.macOsX) {
            targetPlatformName = "mac"
            installerType = "dmg"
            installerOptions += ['--mac-package-name', 'fireporter']
        }

        def arch = System.getProperty("os.arch")
        installerName = "fireporter_$targetPlatformName-${arch}_v"
    }
}

tasks.register('packImage') {
    dependsOn jpackageImage
    doLast {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def targetPlatformName = currentOs.windows ? "win" : currentOs.linux ? "linux" : "mac"
        def arch = System.getProperty("os.arch")
        def archiveExt = currentOs.windows ? "zip" : "tar.gz"
        def archiveFileName = "fireporter_${targetPlatformName}-${arch}-portable_v-${project.version}.${archiveExt}"

        def outputDir = file("$buildDir/distributions/image")
        outputDir.mkdirs()

        if (currentOs.windows) {
            def imageDir = file("$buildDir/distributions/image/Fireporter")
            ant.zip(destfile: "${outputDir}/${archiveFileName}", basedir: imageDir)
        } else if (currentOs.linux) {
            def imageDir = file("$buildDir/distributions/image/fireporter")
            ant.tar(destfile: "${outputDir}/${archiveFileName}", compression: "gzip") {
                tarfileset(dir: imageDir)
            }
        } else if (currentOs.macOsX) {
            def imageDir = file("$buildDir/distributions/image/Fireporter.app")
            ant.tar(destfile: "${outputDir}/${archiveFileName}", compression: "gzip") {
                tarfileset(dir: imageDir)
            }
        }
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.fadlan.fireporter.FireporterApp',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Fadlan Abduh'
        )
    }
    from sourceSets.main.output
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}